plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'org.jetbrains.kotlin.jvm'
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.6.10'
}

group 'com.alsatpardakht'
version '1.0.0'

java {
    sourceCompatibility = JavaVersion.VERSION_1_7
    targetCompatibility = JavaVersion.VERSION_1_7
    withJavadocJar()
    withSourcesJar()
}

dependencies {

    // Coroutines
    def coroutines_version = "1.5.2"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

    // Ktor
    def ktor_version = "1.6.3"
    implementation "io.ktor:ktor-client-core:$ktor_version"
    implementation "io.ktor:ktor-client-cio:$ktor_version"
    implementation "io.ktor:ktor-client-serialization:$ktor_version"
    implementation "io.ktor:ktor-client-logging:$ktor_version"

    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.3.0"
}

publishing {
    publications{

        mavenJava(MavenPublication){

            groupId = 'com.alsatpardakht'
            artifactId = 'alsatipgcore'
            version = '1.0.0'
            from components.java

            pom {
                name = 'AlsatIPGCore'
                description = 'AlsatIPGCore is an open-source Kotlin library to connect Alsat pardakht peyment API'
                url = 'https://github.com/AlsatPardakht/AlsatIPGCore'
                inceptionYear = '2022'

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'http://www.opensource.org/licenses/mit-license.php'
                    }
                }
                developers {
                    developer {
                        id = 'erfanmhat'
                        name = 'erfan mahdavi athar'
                        email = 'erfan.mh.at@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git:github.com/AlsatPardakht/AlsatIPGCore'
                    developerConnection = 'scm:git:ssh://github.com/AlsatPardakht/AlsatIPGCore'
                    url = 'https://github.com/AlsatPardakht/AlsatIPGCore'
                }
            }
        }
    }

    repositories {
        maven {
            name = "OSSRH"
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = project.properties["ossrhUsername"]
                password = project.properties["ossrhPassword"]
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}